// Block definition
FUNCTION_BLOCK recomendador

// Define input variables
VAR_INPUT
    edad : REAL;
    familiaridad : REAL;
    hInvertible : REAL;
    pAbandono : REAL;
END_VAR

// Define output variable
VAR_OUTPUT
    dRecomendacion : REAL;
    tExigido : REAL;
END_VAR

// Fuzzify variable de entrada 'edad'
FUZZIFY edad
    TERM Ninos := (0,1) (2,1) (14,0);
    TERM Adolescentes := (12,0) (14,1) (17.5,1) (18,0);
    TERM Jovenes := (16,0) (21.4,1) (23,1) (25,0);
    TERM Adultos := (21,0) (30,1) (55,1) (65,0);
    TERM Ancianos := (60,0) (64,1) (80,1) (105,0);
END_FUZZIFY

// Fuzzify variable de entrada 'familiaridad'
FUZZIFY familiaridad
    TERM Nula := (0,1) (2.5,1) (5,0);
    TERM Poca := (3,0) (16.86,1) (25,1) (27,0);
    TERM Algo_familiarizado := (25,0) (39.1,1) (46.06,1) (52,0);
    TERM Muy_familiarizado := (48,0) (59.86,1) (65.8,1) (77,0);
    TERM Experto := (64,0) (84.7,1) (100,1);
END_FUZZIFY

// Fuzzify variable de entrada 'hInvertible'
FUZZIFY hInvertible
    TERM Muy_pocas := (0,1) (0.7999,1) (3,0);
    TERM Pocas := (2,0) (3,1) (7.5,1) (8,0);
    TERM Medias := (5.5,0) (11,1) (14,1) (15,0);
    TERM Muchas := (13,0) (22,1) (24,1); //No lo zhe rick
END_FUZZIFY

// Fuzzify variable de entrada 'pAbandono'
FUZZIFY pAbandono
    TERM Muy_bajo := (0,1) (1.5,1) (13.5,0);
    TERM Bajo := (10,0) (23.14,1) (25.14,1) (30,0);
    TERM Medio := (25,0) (32,1) (46,1) (60,0);
    TERM Alto := (55,0) (72,1) (75,1) (85,0);
    TERM Muy_alto := (75,0) (97.5,1) (100,1);
END_FUZZIFY


// Defzzzify output variable 'dRecomendacion'
DEFUZZIFY dRecomendacion
    TERM Facil := (0,1) (3.057,1) (5,0);
    TERM Medio := (4,0) (6,1) (7.334,1) (11,0);
    TERM Dificil := (9,0) (12.13,1) (15,1) (16,0);
    TERM Muy_dificil := (14,0) (20,1);
    // Usar 'Centro de Gravedad' como metodo de defuzificacion
    METHOD : COG;
    // El valor por defecto es 0 si ninguna regla aplica
    DEFAULT := 0;
END_DEFUZZIFY

// Defzzzify output variable 'tExigido'
DEFUZZIFY tExigido
    TERM Muy_poco := (0,1) (4,1) (6,0);
    TERM Poco := (5,0) (8.6,1) (12.3,1) (13,0);
    TERM Medio := (12,0) (17,1) (24,1) (27,0);
    TERM Mucho := (25,0) (34.87,1) (41.3,1) (52,1);
    // Usar 'Centro de Gravedad' como metodo de defuzificacion
    METHOD : COG;
    // El valor por defecto es 10 si ninguna regla aplica
    DEFAULT := 0;
END_DEFUZZIFY

RULEBLOCK No1
    // Use 'min' for 'and' (also implicit use 'max'
    // for 'or' to fulfill DeMorgan's Law)
    AND : MIN;
    ACT : MIN;  // Use 'min' activation method
    ACCU : MAX; // Use 'max' accumulation method

    RULE 1 : IF hInvertible IS Muchas 
            THEN tExigido IS Mucho;
    RULE 2 : IF hInvertible IS Medias
            THEN tExigido IS Medio;
    RULE 3 : IF hInvertible IS Pocas
            THEN tExigido IS Poco;
    RULE 4 : IF hInvertible IS Muy_pocas 
            THEN tExigido IS Muy_poco;

    RULE 5 : IF familiaridad IS Nula
            THEN dRecomendacion IS Facil;
    RULE 6 : IF familiaridad IS Poca
            THEN dRecomendacion IS Facil;
    RULE 7 : IF familiaridad IS Algo_familiarizado
            THEN dRecomendacion IS Medio;
    RULE 8 : IF familiaridad IS Muy_familiarizado
            THEN dRecomendacion IS Dificil;
    RULE 9 : IF familiaridad IS Experto
            THEN dRecomendacion IS Muy_dificil;

    RULE 10 : IF edad IS Ninos 
            THEN dRecomendacion IS Facil;
    RULE 11 : IF edad IS Adolescentes
            THEN dRecomendacion IS Medio;
    RULE 12 : IF edad IS Adultos
            THEN dRecomendacion IS Dificil;
    RULE 13 : IF edad IS Ancianos 
            THEN dRecomendacion IS Dificil;

    RULE 14 : IF pAbandono IS Medio
            THEN dRecomendacion IS Medio, tExigido IS Poco;
    RULE 15 : IF pAbandono IS Alto
            THEN dRecomendacion IS Medio, tExigido IS Poco;
    RULE 16 : IF pAbandono IS Muy_alto
            THEN dRecomendacion IS Facil, tExigido IS Muy_poco;

END_RULEBLOCK

END_FUNCTION_BLOCK